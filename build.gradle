apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

sourceCompatibility = 1.6

group = 'com.box'
archivesBaseName = 'box-java-sdk'
version = '2.2.1-SNAPSHOT'

repositories {
    mavenCentral()
}



javadoc {vvvvvvvvvvvvvvvv
    options.noHelp true
    options.noDeprecatedList true
    options.noNavBar true
    options.docEncoding 'utf-8'
    options.charSet 'utf-8'
    options.linkSource true
    options.links 'https://docs.oracle.com/javase/8/docs/api/'
}

checkstyleMain { classpath += configurations.compile }
checkstyleTest { classpath += configurations.compile }

sourceSets {
    samples {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.runtimeClasspath
        }
        gretty {
          contextPath = '/'
        }
    }
}

dependencies {
    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.1'
    compile 'org.bitbucket.b_c:jose4j:0.4.4'
    compile 'org.bouncycastle:bcprov-jdk15on:1.52'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.52'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.github.tomakehurst:wiremock:1.52'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.slf4j:slf4j-api:1.7.7'
    testCompile 'org.slf4j:slf4j-nop:1.7.7'
    samplesCompile 'javax.servlet:javax.servlet-api:3.0.1'
    samplesCompile 'com.box:box-java-sdk:2.1.0'
    samplesCompile 'com.mashape.unirest:unirest-java:1.4.5'
}

task runExample(type: JavaExec, dependsOn: 'samplesClasses') {
    classpath = sourceSets.samples.runtimeClasspath
    main = 'com.box.sdk.samples.Main'
}

task runCreateAppUser(type: JavaExec, dependsOn: 'samplesClasses') {
    classpath = sourceSets.samples.runtimeClasspath
    main = 'com.box.sdk.samples.CreateAppUser'
}

task runAccessAsAppUser(type: JavaExec, dependsOn: 'samplesClasses') {
    classpath = sourceSets.samples.runtimeClasspath
    main = 'com.box.sdk.samples.AccessAsAppUser'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task integrationTest(type: Test) {
    description 'Runs the integration tests.'
    group 'Verification'

    useJUnit {
        includeCategories 'com.box.sdk.IntegrationTest'
    }
}

jacocoTestReport.dependsOn(integrationTest);

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'

    if (project.hasProperty('bootClasspath')) {
        options.bootClasspath = bootClasspath
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }

    outputs.upToDateWhen { false }
}

artifacts {
    archives sourcesJar, javadocJar
}

test {
    useJUnit {
        excludeCategories 'com.box.sdk.IntegrationTest'
    }
}

gretty {
    port = 8081
}

signing {
    required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            pom.project {
                name 'Box Java SDK'
                packaging 'jar'
                description 'The Box SDK for Java.'
                url 'http://opensource.box.com/box-java-sdk/'

                scm {
                    connection 'scm:git:https://github.com/box/box-java-sdk.git'
                    developerConnection 'scm:git:https://github.com/box/box-java-sdk.git'
                    url 'https://github.com/box/box-java-sdk'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'gcurtis'
                        name 'Greg Curtis'
                        email 'gcurtis@box.com'
                    }
                }
            }
        }
    }
}
