apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

group = 'com.box'
archivesBaseName = 'box-java-sdk'
version = '0.6.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.1'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.github.tomakehurst:wiremock:1.52'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.slf4j:slf4j-api:1.7.7'
    testCompile 'org.slf4j:slf4j-nop:1.7.7'
}

javadoc {
    options.windowTitle 'Box Java SDK'
    options.noQualifiers 'all'
    options.stylesheetFile file('doc/css/javadoc.css')
    options.noTree true
    options.noIndex true
    options.noHelp true
    options.noDeprecatedList true
    options.noNavBar true
    options.docEncoding 'utf-8'
    options.charSet 'utf-8'
    options.linkSource true
    options.links 'https://docs.oracle.com/javase/8/docs/api/'
}

sourceSets {
    example {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.runtimeClasspath
        }
    }
}

task runExample(type: JavaExec, dependsOn: 'exampleClasses') {
    classpath = sourceSets.example.runtimeClasspath
    main = 'com.box.sdk.example.Main'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task integrationTest(type: Test) {
    description 'Runs the integration tests.'
    group 'Verification'

    useJUnit {
        includeCategories 'com.box.sdk.IntegrationTest'
    }
}

jacocoTestReport.dependsOn(integrationTest);

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
        testLogging.showStandardStreams = true
    }

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

test {
    useJUnit {
        excludeCategories 'com.box.sdk.IntegrationTest'
    }
}

signing {
    required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            pom.project {
                name 'Box Java SDK'
                packaging 'jar'
                description 'The Box SDK for Java.'
                url 'http://opensource.box.com/box-java-sdk/'

                scm {
                    connection 'scm:git:https://github.com/box/box-java-sdk.git'
                    developerConnection 'scm:git:https://github.com/box/box-java-sdk.git'
                    url 'https://github.com/box/box-java-sdk'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'gcurtis'
                        name 'Greg Curtis'
                        email 'gcurtis@box.com'
                    }
                }
            }
        }
    }
}
