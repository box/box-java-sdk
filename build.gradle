plugins {
    id 'com.github.nbaztec.coveralls-jacoco' version '1.2.13'
}

apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 1.8

group = 'com.box'
archivesBaseName = 'box-java-sdk'
version = '2.58.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    example {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.runtimeClasspath
        }
    }
    intTest {
        java {
            compileClasspath += sourceSets.main.output
            compileClasspath += sourceSets.test.output
            runtimeClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.test.output
        }
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    compile 'org.bitbucket.b_c:jose4j:0.7.9'
    // Do not update to latest version, as v1.57 is needed for FIPS compliance purposes
    compile 'org.bouncycastle:bcprov-jdk15on:1.57'
    // Do not update to latest version, as v1.57 is needed for FIPS compliance purposes
    compile 'org.bouncycastle:bcpkix-jdk15on:1.57'
    testCompile 'junit:junit:4.13.2'
    testCompile 'org.hamcrest:hamcrest-library:2.2'
    testCompile 'com.github.tomakehurst:wiremock:2.27.2'
    testCompile 'org.mockito:mockito-core:4.1.0'
    testCompile 'org.slf4j:slf4j-nop:1.7.32'
    exampleCompile 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    checkstyle 'com.puppycrawl.tools:checkstyle:6.19'
    intTestImplementation 'junit:junit:4.13.2'
    intTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    intTestImplementation 'org.mockito:mockito-core:4.1.0'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

javadoc {
    options.windowTitle 'Box Java SDK'
    options.noQualifiers 'all'
    options.stylesheetFile file('doc/css/javadoc.css')
    options.noTree true
    options.noIndex true
    options.noHelp true
    options.noDeprecatedList true
    options.noNavBar true
    options.encoding 'utf-8'
    options.docEncoding 'utf-8'
    options.charSet 'utf-8'
    options.linkSource true
    options.links 'https://docs.oracle.com/javase/8/docs/api/'
}

checkstyleMain { classpath += configurations.compile }
checkstyleTest { classpath += configurations.compile }

task runExample(type: JavaExec, dependsOn: 'exampleClasses') {
    classpath = sourceSets.example.runtimeClasspath
    main = 'com.box.sdk.example.Main'
}

task runCreateAppUser(type: JavaExec, dependsOn: 'exampleClasses') {
    classpath = sourceSets.example.runtimeClasspath
    main = 'com.box.sdk.example.CreateAppUser'
}

task runAccessAsAppUser(type: JavaExec, dependsOn: 'exampleClasses') {
    classpath = sourceSets.example.runtimeClasspath
    main = 'com.box.sdk.example.AccessAsAppUser'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task integrationTest(type: Test) {
    description 'Runs the integration tests.'
    group 'Verification'
    testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
}

//TODO: enable JWT tests
//task integrationTestJWT(type: Test) {
//    description 'Runs the JWT based integration tests.'
//    group 'Verification'
//    testLogging.showStandardStreams = true
//    useJUnit {
//        includeCategories 'com.box.sdk.IntegrationTestJWT'
//    }
//}

jacoco {
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'

    if (project.hasProperty('bootClasspath')) {
        options.bootClasspath = bootClasspath
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }

    outputs.upToDateWhen { false }
}

artifacts {
    archives sourcesJar, javadocJar
}

test {
    useJUnit {
        excludeCategories 'com.box.sdk.IntegrationTestJWT'
    }
}

signing {
    required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            pom.project {
                name 'Box Java SDK'
                packaging 'jar'
                description 'The Box SDK for Java.'
                url 'http://opensource.box.com/box-java-sdk/'

                scm {
                    connection 'scm:git:https://github.com/box/box-java-sdk.git'
                    developerConnection 'scm:git:https://github.com/box/box-java-sdk.git'
                    url 'https://github.com/box/box-java-sdk'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'gcurtis'
                        name 'Greg Curtis'
                        email 'gcurtis@box.com'
                    }
                }
            }
        }
    }
}

idea {
    module {
        sourceDirs -= file('src/intTest/java')
        testSourceDirs += file('src/intTest/java')
    }
}
